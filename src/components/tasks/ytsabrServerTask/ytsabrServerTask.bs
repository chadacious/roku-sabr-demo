import "pkg:/source/logger.bs"
import "pkg:/source/http.bs"
import "pkg:/source/SabrStreamingAdapter.bs"
import "pkg:/source/SabrDebug.bs"
import "pkg:/source/SabrUtils.bs"
import "pkg:/source/taskUtils.bs"

sub init()
    m.logs = logger()
    m.top.functionName = "YtsabrServer"
    m.connections = {}
    m.sockets = {}
    m.ut = CreateObject("roUrlTransfer")
    m.ut.RetainBodyOnError(true)
    m.ut.enableEncodings(true)
    m.ut.setRequest("GET")
    m.ut.SetCertificatesFile("common:/certs/ca-bundle.crt")
    m.ut.initClientCertificates()
    sabr_log(m.logs, log_level_Type.DEBUG, "[YTSABR-Server] Initializing YtsabrServer...")
    m.sabrPayloads = {}
    m.contentType = invalid
end sub

' Task entry point for the SABR HTTP server (video or audio). Accepts
' incoming TCP connections, delegates to the HTTP parser, and feeds requests
' into the SABR streaming adapter.
sub YtsabrServer()
    port = CreateObject("roMessagePort")
    tcp = createobject("roStreamSocket")
    tcp.setMessagePort(port)
    tcp_bind_addr = CreateObject("roSocketAddress")
    tcp_bind_addr.setPort(m.top.contentType = "AUDIO" ? 7012 : 7011)
    m.contentType = m.top.contentType
    sabr_log(m.logs, log_level_Type.INFO, `[YTSABR-Server] Starting YtsabrServer: ${m.top.contentType} on port ${tcp_bind_addr.getPort()}`)
    tcp.setAddress(tcp_bind_addr)
    tcp.notifyReadable(true)
    tcp.listen(4)
    if not tcp.eOK()
        sabr_log(m.logs, log_level_Type.WARN, "[YTSABR-Server] Could not create TCP socket", false)
        ' stop
    end if
    sabr_log(m.logs, log_level_Type.INFO, `[YTSABR-Server] address is ${tcp_bind_addr.getAddress()}`)

    while(true)
        msg = wait(0, port)
        msgType = type(msg)
        ' if msgType <> "Invalid"
        '     sabr_log(m.logs, log_level_Type.VERBOSE, `[YTSABR-Server] Event received type=${msgType}`)
        ' end if
        if msgType = "roSocketEvent"
            try
                socketId = msg.getSocketID()
                if socketId = tcp.getID()
                    client = tcp.accept()
                    if client = invalid
                        ' sabr_log(m.logs, log_level_Type.WARN, `[YTSABR-Server] Accept failed on listen socket ${tcp.getID()}`, false)
                    else
                        client.notifyReadable(true)
                        client.setMessagePort(port)
                        m.sockets[Stri(client.getID())] = client
                    end if
                else
                    ' Must be a client connection!
                    connection = m.sockets[Stri(socketId)]
                    ' If connection is invalid, what does that mean?
                    if isValid(connection)
                        ' FIXME: Do we actually correctly close sockets?
                        safeCountRcv = invalid
                        errOnCount = false
                        if connection.isReadable()
                            try
                                safeCountRcv = connection.getCountRcvBuf()
                            catch e
                                errOnCount = true
                                safeCountRcv = -1
                            end try
                        end if

                        if connection.isReadable() and (safeCountRcv = 0 or errOnCount) and connection.isListening() = false then
                            ' Apparently this means the connection has been closed
                            ' What a terrible way to indicate it
                            sabr_log(m.logs, log_level_Type.INFO, `[YTSABR-Server] Connection ${connection.getID()} is closed`)
                            connection.close()
                            m.sockets[Stri(msg.getSocketID())] = invalid
                        else
                            logSocketStatus(connection)
                            ' print "tcp " ; socketId ; connection.eOK() ; connection.status() ; connection.isConnected() ; connection.isReadable() ; connection.getCountRcvBuf() = 0
                            handle_tcp(connection, port)
                        end if
                    ' else
                    '     ' Stale event for a connection we already closed; ignore instead of spamming WARNs.
                    '     sabr_log(m.logs, log_level_Type.DEBUG, `[YTSABR-Server] Ignoring socket event for closed id=${socketId}`)
                    end if
                end if
            catch e
                sabr_log(m.logs, log_level_Type.WARN, "[YTSABR-Server] emergency abort roSocket handling", false)
                sabr_log(m.logs, log_level_Type.WARN, FormatJson(e), false)
            end try
        end if
    end while
end sub

'------------------------------------------------------------------------------
' Function : sabr_streamUMPPayload
' Purpose  : Stream a SABR spool response back to the Roku player, supporting
'            both cached byte arrays and on-disk chunk lists.
'------------------------------------------------------------------------------
function sabr_streamUMPPayload(connection as object, sabrResponse as dynamic, requestLogTag as string) as integer
    path = sabrResponse.path
    chunks = sabrResponse?.chunks
    totalBytes = sabr_toInt(sabrResponse?.totalBytes)
    payloadStart = sabrResponse.payloadStart
    payloadSize = sabrResponse.payloadSize
    streamStatus = 0
    responseHeaders = sabrResponse.headers
    if not isValid(responseHeaders)
        responseHeaders = CreateObject("roAssociativeArray")
    end if
    statusCode = sabrResponse.status
    if not isValid(statusCode)
        statusCode = 206
    end if

    dataBlob = invalid
    if not IsArray(chunks) or chunks.count() = 0
        dataBlob = sabrResponse?.data
        if GetInterface(dataBlob, "ifByteArray") = invalid and path <> ""
            dataBlob = invalid
        end if
        if GetInterface(dataBlob, "ifByteArray") <> invalid
            totalBytes = dataBlob.count()
            responseHeaders["Content-Length"] = totalBytes.toStr()
            coverage = sabrResponse?.coverage
            coverageStart = sabr_toInt(coverage?.rangeStart)
            coverageEnd = sabr_toInt(coverage?.rangeEnd)
            if coverageStart = invalid then coverageStart = 0
            if coverageEnd = invalid and totalBytes > 0
                coverageEnd = coverageStart + totalBytes - 1
            end if
            coverageLabel = "bytes ?-?"
            if coverageStart <> invalid and coverageEnd <> invalid
                coverageLabel = `bytes ${coverageStart}-${coverageEnd}`
                responseHeaders["Content-Range"] = `${coverageLabel}/${coverageEnd + 1}`
            end if
            sourceLabel = "init_cache"
            sabr_log(m.logs, log_level_Type.INFO, `${requestLogTag} Streaming ${sourceLabel} blob range=${coverageLabel} bytes=${totalBytes}`)
            if http_sendHeaders(connection, statusCode, responseHeaders) = false
                return -1
            end if
            sent = connection.send(dataBlob, 0, dataBlob.count())
            if sent <= 0
                sabr_log(m.logs, log_level_Type.DEBUG, `${requestLogTag} Client closed socket mid-body (blob) status=${sent}`, false)
                streamStatus = sent
            else
                streamStatus = sent
            end if
            cleanupPath = sabr_toSafeString(sabrResponse?.cleanupPath)
            if cleanupPath <> ""
                sabr_deleteFileIfExists(cleanupPath)
            end if
            return streamStatus
        end if
    end if

    if totalBytes = invalid
        totalBytes = 0
        if IsArray(chunks)
            for each chunk in chunks
                chunkLen = sabr_toInt(chunk?.length)
                if chunkLen <> invalid then totalBytes = totalBytes + chunkLen
            end for
        end if
    end if
    if totalBytes = invalid then totalBytes = 0
    responseHeaders["Content-Length"] = totalBytes.toStr()
    coverageLabel = ""
    coverage = sabrResponse?.coverage
    if IsAssociativeArray(coverage)
        startLabel = sabr_valueToLabel(coverage.Lookup("rangeStart"))
        endLabel = sabr_valueToLabel(coverage.Lookup("rangeEnd"))
        if startLabel <> "" and endLabel <> ""
            coverageLabel = `bytes ${startLabel}-${endLabel}`
        end if
    end if
    if coverageLabel = "" then coverageLabel = "bytes ?-?"
    sourceLabel = sabrResponse?.servedFromInitCache = true ? "init_cache" : "sabr_stream"
    sabr_log(m.logs, log_level_Type.INFO, `${requestLogTag} Streaming ${sourceLabel} path=${path} range=${coverageLabel} chunks=${chunks.count()} totalBytes=${totalBytes}`)
    streamStatus = send_http_reply_stream_chunks(connection, statusCode, responseHeaders, path, chunks, totalBytes)
    if streamStatus <= 0
        sabr_log(m.logs, log_level_Type.DEBUG, `${requestLogTag} Streaming reply ended early status=${streamStatus}`, false)
    end if
    cleanupPath = sabr_toSafeString(sabrResponse?.cleanupPath)
    if cleanupPath = "" and sabrResponse?.isInitSeg <> true
        cleanupPath = sabr_toSafeString(path)
    end if
    if cleanupPath <> ""
        sabr_deleteFileIfExists(cleanupPath)
    end if
    return streamStatus
end function

function sabr_hasBlobData(response as object) as boolean
    if not IsAssociativeArray(response) then return false
    dataBlob = response.Lookup("data")
    if GetInterface(dataBlob, "ifByteArray") = invalid then return false
    return dataBlob.count() > 0
end function

sub logSocketStatus(connection as object)
    if connection = invalid then return
    info = {
        "socketId": connection.getID()
        "readable": connection.isReadable()
        "connected": connection.isConnected()
        "recvBufBytes": -1
        "status": connection.status()
        "eConnAborted": connection.eConnAborted()
        "eConnRefused": connection.eConnRefused()
        "eConnReset": connection.eConnReset()
        "eIsConn": connection.eIsConn()
        "eNotConn": connection.eNotConn()
    }
    if connection.isReadable()
        try
            info["recvBufBytes"] = connection.getCountRcvBuf()
        catch e
            info["recvBufBytes"] = -2
        end try
    end if
    sabr_log(m.logs, log_level_Type.VERBOSE, `[YTSABR-Server] roSocketEvent detail: ${FormatJson(info)}`)
end sub

' Reads data from a client socket until an HTTP request is complete, then
' dispatches it to `process_request`. Manages connection lifetime bookkeeping.
sub handle_tcp(connection as object, port as object)
    request = m.connections[Stri(connection.getID())]
    if request = invalid then
        ' An unsolicited request. Create an http handler for it
        request = create_new_request()
        m.connections[Stri(connection.getID())] = request
    end if
    status = request.read_data(request, connection)
    if status = false then ' More data is required
        sabr_log(m.logs, log_level_Type.INFO, "[YTSABR-Server] Not yet finished")
        return
    else if status = true then 'Data is complete. Execute handler
        ' body = request.body
        request.b64Body = request.body.ToBase64String()
        ' ' print "  body: "request.body
        sabr_log(m.logs, log_level_Type.VERBOSE, `[YTSABR-Server] Examining Request: {${chr(10)}  state: ${request.state},${chr(10)}  path: ${request.path},${chr(10)}  headers: ${FormatJson(request.headers)},${chr(10)}  method: ${request.method},${chr(10)}  body_size: ${request.body_size},${chr(10)}  search: ${FormatJson(request.search)},${chr(10)}  body: ${request.b64Body}${chr(10)}}`)
        ' Regardless of whether the socket is to be closed, the HTTP request has finished. We have to invalidate it here
        ' since the process_data() call might set up something else
        m.connections[Stri(connection.getID())] = invalid
        should_close = process_request(request, connection, port)
        if should_close then
            m.sockets[Stri(connection.getID())] = invalid
            connection.close()
            sabr_log(m.logs, log_level_Type.INFO, `[YTSABR-Server] finalizing connection close: ${connection.getID()}`)
        end if
    else ' Error condition. Not handled (FIXME! Need to return ints instead of booleans so we have a third case. Or invalid?)
        sabr_log(m.logs, log_level_Type.WARN, `[YTSABR-Server] unrecognized status: ${status}`, false)
        ' stop
    end if
end sub

' Handles a fully parsed HTTP request for the SABR server. Intercepts SABR
' segment URLs, loads the cached payload metadata, and forwards the request
' to `sabr_handleRequest`, returning `true` when the socket should close.
function process_request(request as object, connection as object, port as object)
    sabr_log(m.logs, log_level_Type.INFO, `[YTSABR-Server] Processing request: ${request?.path}`)
    ' port = CreateObject("roMessagePort")
    status = invalid
    if request.path.inStr("/sabr/") > -1 then
        sabr_log(m.logs, log_level_Type.INFO, `[YTSABR-Server] Intercepted SABR request: ${request.path}`)

        mediaIdHash = sabr_extractMediaIdHash(request.path)
        if mediaIdHash = invalid then
            sabr_log(m.logs, log_level_Type.WARN, `[YTSABR-Server] Could not extract mediaIdHash from URL: ${request.path} on ${connection.getID()}`, false)
            status = send_http_reply(connection, "400", "text/plain", "Bad Request - could not extract mediaIdHash")
            return true
        end if

        keyParam = ""
        if IsAssociativeArray(request?.search)
            keyParam = sabr_valueToLabel(request.search.Lookup("key"))
        end if
        parsedKey = sabr_parseScopedKey(keyParam)
        if parsedKey = invalid
            sabr_log(m.logs, log_level_Type.WARN, `[YTSABR-Server] Invalid SABR key format: ${keyParam} on ${connection.getID()}`, false)
            status = send_http_reply(connection, "400", "text/plain", "Bad Request - invalid SABR key")
            return true
        end if
        scopeKey = parsedKey.scope

        ' check if we already have this mediaIdHash scoped sabrPayload map loaded
        if m.sabrPayloads[mediaIdHash] = invalid
            filePath = `${sabr_mediaDir(mediaIdHash)}/sabrPayloadMap.json`
            fileContent = getFile(filePath)
            if fileContent = invalid or fileContent = ""
                sabr_log(m.logs, log_level_Type.WARN, `[YTSABR-Server] Could not load sabrPayloadMap.json file: ${filePath} on ${connection.getID()}`, false)
                status = send_http_reply(connection, "503", "text/plain", "Service Unavailable - SABR metadata not ready")
                return true
            end if
            sabrPayloadMap = ParseJson(fileContent)
            if not IsAssociativeArray(sabrPayloadMap)
                sabr_log(m.logs, log_level_Type.WARN, `[YTSABR-Server] sabrPayloadMap.json file is not valid JSON: ${filePath} on ${connection.getID()}`, false)
                status = send_http_reply(connection, "500", "text/plain", "Internal Server Error - sabrPayload map is not valid JSON")
                return true
            end if
            m.sabrPayloads[mediaIdHash] = sabrPayloadMap
            sabr_log(m.logs, log_level_Type.INFO, `[YTSABR-Server-${m.contentType}] Loaded sabrPayloadMap for mediaIdHash: ${mediaIdHash} from ${filePath}`)
        else
            sabrPayloadMap = m.sabrPayloads[mediaIdHash]
            sabr_log(m.logs, log_level_Type.INFO, `[YTSABR-Server-${m.contentType}] Using cached sabrPayloadMap for mediaIdHash: ${mediaIdHash}`)
        end if

        sabrPayload = invalid
        if IsAssociativeArray(sabrPayloadMap)
            sabrPayload = sabrPayloadMap.Lookup(scopeKey)
        end if
        if sabrPayload = invalid
            sabr_log(m.logs, log_level_Type.WARN, `[YTSABR-Server-${m.contentType}] No SABR payload for scope ${scopeKey} (mediaIdHash=${mediaIdHash})`, false)
            status = send_http_reply(connection, "404", "text/plain", "Not Found - SABR scope missing")
            return true
        end if
        rangeHeader = ""
        if IsAssociativeArray(request?.headers)
            rangeHeader = sabr_valueToLabel(request.headers.Lookup("range"))
        end if
        sabr_log(m.logs, log_level_Type.DEBUG, `[YTSABR-Server-${m.contentType}] Request method=${request?.method} path=${request?.path} key=${keyParam} scope=${scopeKey} range=${rangeHeader}`)

        requestId = invalid
        if isValid(m.top?.utilsTask)
            requestId = m.top.utilsTask.callFunc("getNextId", "ytsabrServerRequest")
        end if
        if requestId = invalid
            requestId = Stri(connection.getID())
        end if
        requestLogTag = sabr_utilLogTag(m.contentType, requestId)
        sabrResponse = invalid
        fatalReason = ""
        try
            sabrResponse = sabr_handleRequest(request, m.contentType, mediaIdHash, sabrPayload, m.top.videoNode, invalid)
        catch e
            fatalReason = sabr_formatExceptionDetail(e)
            sabr_log(m.logs, log_level_Type.WARN, `${requestLogTag} sabr_handleRequest exception: ${fatalReason}`, false)
        end try

        if fatalReason <> ""
            status = send_http_reply(connection, "500", "text/plain", fatalReason)
        else if IsAssociativeArray(sabrResponse) and (sabrResponse.DoesExist("chunks") or sabrResponse.DoesExist("payloadStart") or sabr_hasBlobData(sabrResponse))
            status = sabr_streamUMPPayload(connection, sabrResponse, requestLogTag)
        end if
        m.sockets[Stri(connection.getID())] = invalid
        m.connections[Stri(connection.getID())] = invalid
        return true
    else
        sabr_log(m.logs, log_level_Type.WARN, `[YTSABR-Server] Unexpected URI: ${request.path} on ${connection.getID()}`, false)
    end if

    return false ' false = Keep-alive
end function

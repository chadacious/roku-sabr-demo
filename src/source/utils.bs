
function toBase64(rawData)
    b64Text = CreateObject("roByteArray")
    b64Text.FromAsciiString(rawData)
    return b64Text.ToBase64String()
end function

function fromBase64(b64)
    ba = CreateObject("roByteArray")
    ba.fromBase64String(b64)
    tokenData = ba.toAsciiString()
    return tokenData
end function

' Returns lowercase hex digest; algo one of: "md5","sha1","sha256","sha512"
function hashStringEVP(s as string, algo = "sha256" as string) as string
    d = createObject("roEVPDigest")
    if isValid(d) and d.setup(algo) = 0
        ba = createObject("roByteArray")
        ba.fromAsciiString(s)
        d.update(ba)
        return d.final() ' hex string digest
    end if
    return "" ' or fall back to non-crypto below
end function

function generateHexString(length as integer) as string
    hexString = ""
    hexChars = "0123456789abcdef"
    for i = 1 to length
        hexString = hexString + hexChars.Mid(Rnd(length / 2) - 1, 1)
    end for
    return hexString
end function


function ensureDirectoryExistsForPath(filePath as string) as boolean
    ' Walk the path and create missing directories (skip the volume portion like "tmp:/")
    volumeIndex = Instr(1, filePath, ":")
    searchPos = 1
    while true
        slashPos = Instr(searchPos, filePath, "/")
        if slashPos = 0 then exit while

        if volumeIndex > 0 and slashPos <= volumeIndex + 1
            searchPos = slashPos + 1
            continue while
        end if

        dirPath = Left(filePath, slashPos - 1)
        if dirPath <> "" and CreateDirectory(dirPath) = false
            return false
        end if

        searchPos = slashPos + 1
    end while

    return true
end function

function cacheStringToFile(fileName as string, content as string)
    result = { "status": "error", "message": "", "uri": invalid }

    m.logs.printl(log_level_Type.INFO, `[cacheStringToFile] Starting string cache write: ${fileName}`)

    if not ensureDirectoryExistsForPath(fileName)
        result["message"] = `[cacheStringToFile] Failed to create directory for string file: ${fileName}`
        m.logs.printl(log_level_Type.WARN, result["message"])
        return result
    end if

    if writeAsciiFile(fileName, content)
        result["status"] = "ok"
        result["uri"] = fileName
    else
        result["message"] = `[cacheStringToFile] Failed to write string file: ${fileName}`
    end if

    m.logs.printl(log_level_Type.INFO, `[cacheStringToFile] String cache write result: ${FormatJson(result)}`)

    return result
end function

